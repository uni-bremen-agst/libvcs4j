plugins {
	id 'java'
	id 'java-library'
	id 'maven-publish'
	id 'jacoco'
	id 'io.freefair.lombok' version '8.4'
}

group = 'de.uni-bremen.informatik.st'
version = '2.0.1'

repositories {
	mavenCentral()
}

configurations {
	extraLibs
}

dependencies {
	api(project(':libvcs4j-api'))

	// Add local jars from the 'libs' directory.
	extraLibs(fileTree(dir: 'libs', include: '*.jar'))
	// Required by JavaHG.
	api('com.google.guava:guava:32.1.3-jre')

	api('com.ibm.icu:icu4j:74.1')
	api('org.eclipse.jgit:org.eclipse.jgit:6.8.0.202311291450-r')
	api('org.gitlab:java-gitlab-api:4.1.1')
	api('org.kohsuke:github-api:1.318')
	api('org.slf4j:slf4j-api:2.0.9')
	api('org.tmatesoft.svnkit:svnkit:1.10.11')
	testImplementation(project(':testutils'))

	configurations.implementation.extendsFrom(configurations.extraLibs)

	// Upgrade outdated and incompatible transitive package versions
	api('com.fasterxml.jackson.core:jackson-annotations:2.15.3')
	api('com.fasterxml.jackson.core:jackson-core:2.15.3')
	api('com.fasterxml.jackson.core:jackson-databind:2.15.3')
	api('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.3')
	api('commons-io:commons-io:2.15.1')
}

jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}
	afterEvaluate {
		getClassDirectories().from(
				files(classDirectories.files.collect { 
					fileTree(dir: it, exclude: 'bmsi/**') 
				}))
	}
}

tasks.register('sourcesJar', Jar) {
	dependsOn classes
	archiveClassifier = 'sources'
	from delombok
}

javadoc {
	exclude 'bmsi/util'
}

tasks.register('javadocJar', Jar) {
	dependsOn javadoc
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

jar {
	from {
		configurations.extraLibs.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			pom {
				name = 'libvcs4j'
				description = 'A Java Library for Repository Mining'
				url = 'https://github.com/uni-bremen-agst/libvcs4j'
				licenses {
					license {
						name = 'GPL-3.0'
						url = 'https://opensource.org/licenses/GPL-3.0'
					}
				}
				developers {
					developer {
						name = 'Marcel Steinbeck'
						email = 'marcel@informatik.uni-bremen.de'
						organization = 'AG Softwaretechnik (University of Bremen)'
						organizationUrl = 'https://www.informatik.uni-bremen.de/st'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/uni-bremen-agst/libvcs4j.git'
					developerConnection = 'scm:git:ssh://github.com:uni-bremen-agst/libvcs4j.git'
					url = 'https://github.com/uni-bremen-agst/libvcs4j'
				}
			}
			repositories {
				maven {
					url = layout.buildDirectory.dir("repository")
				}
			}
		}
	}
}
