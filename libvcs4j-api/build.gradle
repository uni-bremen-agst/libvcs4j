plugins {
	id 'java'
	id 'maven-publish'
	id 'jacoco'
}

group = 'de.uni-bremen.informatik.st'
version = '2.0.0'

repositories {
	mavenCentral()
}

dependencies {
	testImplementation(project(':testutils'))
}

jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}
}

tasks.register('sourcesJar', Jar) {
	dependsOn classes
	archiveClassifier = 'sources'
	from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
	dependsOn javadoc
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			pom {
				name = 'libvcs4j-api'
				description = 'A Java Library for Repository Mining (API)'
				url = 'https://github.com/uni-bremen-agst/libvcs4j'
				licenses {
					license {
						name = 'MIT License'
						url = 'http://www.opensource.org/licenses/mit-license.php'
					}
				}
				developers {
					developer {
						name = 'Marcel Steinbeck'
						email = 'marcel@informatik.uni-bremen.de'
						organization = 'AG Softwaretechnik (University of Bremen)'
						organizationUrl = 'https://www.informatik.uni-bremen.de/st'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/uni-bremen-agst/libvcs4j.git'
					developerConnection = 'scm:git:ssh://github.com:uni-bremen-agst/libvcs4j.git'
					url = 'https://github.com/uni-bremen-agst/libvcs4j'
				}
			}
			repositories {
				maven {
					url = layout.buildDirectory.dir("repository")
				}
			}
		}
	}
}
