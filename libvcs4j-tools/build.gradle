plugins {
	id 'java'
	id 'java-library'
	id 'maven-publish'
	id 'jacoco'
	id 'io.freefair.lombok' version '8.4'
}

group = 'de.uni-bremen.informatik.st'
version = '2.0.0'

repositories {
	mavenCentral()
}

configurations {
	extraLibs
}

dependencies {
	api(project(':libvcs4j-api'))
	api('fr.inria.gforge.spoon:spoon-core:10.4.2')
	api('net.sourceforge.pmd:pmd-core:6.55.0')
	api('net.sourceforge.pmd:pmd-java:6.55.0')
	api('org.slf4j:slf4j-api:2.0.9')
	extraLibs(project(':conqat'))
	testImplementation(project(':testutils'))
	testImplementation(project(':libvcs4j'))
	configurations.implementation.extendsFrom(configurations.extraLibs)
}

jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}
	afterEvaluate {
		getClassDirectories().from(
				files(classDirectories.files.collect {
					fileTree(dir: it, exclude: 'bmsi/**')
				}))
	}
}

tasks.register('sourcesJar', Jar) {
	dependsOn classes
	archiveClassifier = 'sources'
	from delombok
}

tasks.register('javadocJar', Jar) {
	dependsOn javadoc
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

jar {
	from {
		configurations.extraLibs.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			pom {
				name = 'libvcs4j-tools'
				description = 'LibVCS4j tools'
				url = 'https://github.com/uni-bremen-agst/libvcs4j'
				licenses {
					license {
						name = 'LGPL-3.0'
						url = 'https://opensource.org/licenses/LGPL-3.0'
					}
				}
				developers {
					developer {
						name = 'Marcel Steinbeck'
						email = 'marcel@informatik.uni-bremen.de'
						organization = 'AG Softwaretechnik (University of Bremen)'
						organizationUrl = 'https://www.informatik.uni-bremen.de/st'
					}
					developer {
						name = 'Dominique Schulz'
						email = 'do_sc@uni-bremen.de'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/uni-bremen-agst/libvcs4j.git'
					developerConnection = 'scm:git:ssh://github.com:uni-bremen-agst/libvcs4j.git'
					url = 'https://github.com/uni-bremen-agst/libvcs4j'
				}
			}
			repositories {
				maven {
					url = layout.buildDirectory.dir("repository")
				}
			}
		}
	}
}
